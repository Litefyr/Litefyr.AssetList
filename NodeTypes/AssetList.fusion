prototype(Jonnitto.AssetList:Component.Collection) {
    @process.addHeadline = afx`
        <Litefyr.Presentation:Atom.HeadlineAndLabel
            @if={!props.enableText && props.enableHeadline}
            content={props.headline}
            label={props.label}
            inBackend={node.context.inBackend}
            class='headline-alignment'
            isFirstElement={false}
            fontSize={props.headlineFontSize}
            marginBottom={true}
        />
        {value}
    `
}

prototype(Jonnitto.AssetList:Component.Action) {
    caption = null
}

prototype(Jonnitto.AssetList:Component.Asset.Title) {
    renderer.@process.addCaption = afx`
        {value}
        <small @if={props.asset.caption} class="block">{props.asset.caption}</small>
    `
}

prototype(Jonnitto.AssetList:AssetList) {
    breakout = ${q(node).property('breakout')}
    enableText = ${q(node).property('enableText')}
    float = ${q(node).property('float')}
    textMobileBottom = ${q(node).property('textMobileBottom')}
    enableHeadline = ${q(node).property('enableHeadline')}
    headlineFontSize = ${q(node).property('headlineFontSize')}
    leftColSize = ${q(node).property('leftColSize')}
    textLeft = ${q(node).property('textLeft')}
    theme = ${q(node).property('theme')}
    align = ${q(node).property('align')}
    links = Litefyr.Integration:Component.Links

    headline = Neos.Neos:Editable {
        property = 'headline'
        block = false
    }

    label = Neos.Neos:Editable {
        property = 'label'
        block = false
    }

    text = Neos.Neos:Editable {
        property = 'text'
    }

    animationElements = Litefyr.Animations:Helper.Elements {
        selector = 'h1,h2,li'
        stagger = ${q(node).property('thumbnail')}
    }
    animationText = Litefyr.Animations:Helper.Text

    attributes = Carbon.FileLoader:Strategy.Attributes {
        package = 'Litefyr.AssetList'
        css = 'Main.css'
    }

    renderer.@process {
        spacing = afx`
            <Litefyr.Presentation:Spacing
                @if={!props.enableText}
                augmenter
                padding
                tailwind="not-prose"
                width={props.breakout}
                animationElements={props.animationElements}
                class={Presentation.cssClass("asset-list")}
            />
        `

        textWithContent = afx`
            <Litefyr.Presentation:Module.TextWithContent
                @if={props.enableText}
                breakpoint={props.thumbnail ? 'lg' : 'md'}
                {...props}
                content={value}
                wrapperClass={Presentation.cssClass("asset-list")}
            />
        `
    }
}
